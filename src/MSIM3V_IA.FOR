C PROGRAM MSIM3V_IA
C
C MATERIAL SIMULATION WITH 3-COMPONENT SYSTEMS
C AND CALCULATION OF EQUIVALENT
C PARTIAL AND TOTAL ATTENUATION COEFFICIENTS
C DERIVATIVE FORMULATION,
C MODIFIED 10/21/91 TO ALLOW VARIABLE RELATIVE THICKNESS,
C MODIFIED 1/29/2009 TO WRITE SHARED FILES FOR PLOTTING BY EXCEL
      DIMENSION F(20,5),Z(20,5)
      DIMENSION NL(5),NZ(5),RHO(5)
      DIMENSION MU(150,5),DEMU(150,4)
      DIMENSION MUIS(150,5),MUCS(150,5),MUPE(150,5),MUS(150,5)
      DIMENSION TF(150,3),TR(150),SR(150)
      DIMENSION TFM(3),TFMN(3)
      DIMENSION SIR(150),SCR(150),PER(150)
      DIMENSION ATH2(150),ATH3(150),ATH4(150)
      DIMENSION S(7),DS(5),EN(150)
      CHARACTER IDATE*9,IYR*4,IMON*3,IDAY*2,NAME(5)*40
      INTEGER   PE,Z
      REAL      MU,MUS,MUIS,MUCS,MUPE,ISC
      DIMENSION TOT(10,150,4),DETOT(10,150,4)
      DIMENSION ISC(10,150,4),CSC(10,150,4),CPE(10,150,4) 
      IPASS = 0
C
C SEE SUBROUTINE MUCALC FOR USE OF IZ
C
      IZ = 0
C
C       GET THE DATE (THIS IS A COMPAQ FORTRAN EXTENSION)
C
      CALL DATE(IDATE)
	IDAY = IDATE(1:2)
	IMON = IDATE(4:6)
	IYR  = ('20'//IDATE(8:9))
C
C CHOOSE ENERGY RANGE AND STEP SIZE
C
      WRITE(*,1010)
 1010 FORMAT(/,1X,'SET UP ENERGY GRID FOR CALCULATIONS:',
     1/,1X,'GIVE THE MINIMUM ENERGY, MAXIMUM ENERGY,',
     2/,1X,'AND THE ENERGY STEP SIZE.  FOR EXAMPLE,'
     3/,1X,'10, 150, 10 WOULD GIVE A GRID CONSISTING OF',
     4/,1X,'10, 20, 30, ..., 140, AND 150 KEV.',
     5/,1X,'THE MINIMUM ENERGY ALLOWED BY THE PROGRAM IS 10 KEV.',
     6//,1X,'ALGORITHM TIME DEPENDS LINEARLY ON NUMBER OF POINTS.',
     7/,1X,'MAXIMUM NUMBER OF ENERGY VALUES ALLOWED IS 150.')
      WRITE(*,1020)
 1020 FORMAT(//,1X'ENTER MIN, MAX ENERGY, DELTA')
      READ(*,*) EMIN,EMAX,DELTA
      IF (EMIN.LT.10.0)  EMIN = 10.0
      IF (DELTA.LE.0.) DELTA = 1.0
   15 NEN = (EMAX-EMIN)/DELTA + 1.0
      IF (NEN.LE.150.AND.NEN.GT.0) GO TO 20
      DELTA = DELTA + 1.0
      GO TO 15
   20 WRITE(*,1030) NEN,EMIN,EMAX,DELTA
 1030 FORMAT(/,1X,I3,' ENERGY POINTS, FROM',F6.1,' KEV TO',F6.1,
     1  ' KEV IN STEPS OF',F6.1,' KEV',//)
C
C SET UP ENERGY GRID
C
      DO 30 K=1,NEN
      EN(K) = EMIN + (K-1)*DELTA
   30 CONTINUE
C
C OPEN SHARED FILES FOR PLOTTING BY EXCEL
C
      OPEN(1,FILE='PL1.TXT',FORM='FORMATTED',SHARED)
      OPEN(2,FILE='PL2.TXT',FORM='FORMATTED',SHARED)
C      OPEN(3,FILE='PL3.TXT',FORM='FORMATTED',SHARED)
C      OPEN(4,FILE='PL4.TXT',FORM='FORMATTED',SHARED)
C      OPEN(5,FILE='PL5.TXT',FORM='FORMATTED',SHARED)
C
C CALCULATION OF ATTENUATION COEFFICIENTS
C FOR THE FOUR MATERIALS
C
      WRITE(*,1040)
 1040 FORMAT(1X,'MATERIAL #1 IS THE ONE TO BE SIMULATED',/,
     2 1X,'MATERIALS 2, 3, AND 4 ARE THE PHANTOM MATERIALS.',//,
     3 1X,'YOU CAN USE CHEMICAL FORMULA OR MASS FRACTIONS',/,
     4 1X,'TO SPECIFY EACH MATERIAL.  FOR WATER, CHEMICAL',/,
     5 1X,'INPUT WOULD BE 1,2, INDICATING 2 ATOMS OF HYDROGEN,',/,
     6 1X,'AND 8,1 FOR 1 ATOM OF OXYGEN.  MASS FRACTION INPUTS',/,
     7 1X,'WOULD BE 1,.1119 AND 8,.8881.  FOR COMPOUNDS AND',/,
     8 1X,'MIXTURES, YOU MUST SUPPLY THE DENSITY.  FOR ELEMENTS,',/,
     9 1X,'THE DENSITY IS CONTAINED IN THE COEFFICIENT DATA SET.',/,
     1 1X,'THERE IS A LIMIT OF 10 ELEMENTS FOR EACH MATERIAL.',/,
     2 1X,'SEE SUBROUTINES MATIN AND MUCALC FOR MORE INFO.',//)
      WRITE(*,50)
   50 FORMAT(//,1X,'ENTER RATIO OF PHANTOM TO OBJECT THICKNESS')
      READ(*,*)V
      IST = 1
      IEND = 4
  100 CONTINUE
      DO 190 I=IST,IEND
      CALL MATIN(I,NAME(I),NL(I),NZ(I),Z(1,I),F(1,I),RHO(I))
      IZ = 0
C
C DO THIS LOOP FOR EACH ELEMENT IN MATERIAL # I
C
      DO 160 II=1,NZ(I)
C
C GENERATE COEFFICIENTS FOR ELEMENT # I
C
  140 DO 150 K=1,NEN
      CALL MUCALC(Z(II,I),EN(K),S,DS,IZ)
      TOT(II,K,I) = S(5)
      DETOT(II,K,I) = DS(5)
      ISC(II,K,I) = S(2)
      CSC(II,K,I) = S(1)
      CPE(II,K,I) = S(4)
  150 CONTINUE
  160 CONTINUE
C
C SET ALL MU'S FOR MATERIAL # I TO 0.
C
      DO 180 K=1,NEN
      MU(K,I) = 0.
      MUS(K,I) = 0.
      DEMU(K,I) = 0.
      MUIS(K,I) = 0.
      MUCS(K,I) = 0.
      MUPE(K,I) = 0.
C
C NOW CALCULATE MU'S USING SUM RULE
C
      DO 170 II=1,NZ(I)
      MU(K,I) = MU(K,I) + F(II,I)*TOT(II,K,I)
      DEMU(K,I) = DEMU(K,I) + F(II,I)*DETOT(II,K,I)
      MUIS(K,I) = MUIS(K,I) + F(II,I)*ISC(II,K,I)
      MUCS(K,I) = MUCS(K,I) + F(II,I)*CSC(II,K,I)
      MUPE(K,I) = MUPE(K,I) + F(II,I)*CPE(II,K,I)
  170 CONTINUE
C
C CONVERT FROM MASS TO LINEAR ATTENUATION COEFFICIENTS
C
      MU(K,I) = MU(K,I)*RHO(I)
      DEMU(K,I) = DEMU(K,I)*RHO(I)
      MUIS(K,I) = MUIS(K,I)*RHO(I)
      MUCS(K,I) = MUCS(K,I)*RHO(I)
      MUPE(K,I) = MUPE(K,I)*RHO(I)
      MUS(K,I) = MUIS(K,I) + MUCS(K,I)
  180 CONTINUE
  190 CONTINUE
      IF (IST.NE.1.OR.IEND.NE.4) GO TO 610
C
C CALCULATE RELATIVE THICKNESSES FOR EACH ENERGY
C THE ALGORITHM REPRESENTED BY THIS
C SECTION OF CODE IS THE 'GUTS' OF
C THIS PROGRAM
C
  300 CONTINUE
      DO 310 K=1,NEN
C
      A = DEMU(K,3) - DEMU(K,4)
      B = MU(K,4) - MU(K,3)
      C = MU(K,3)*DEMU(K,4) - MU(K,4)*DEMU(K,3)
      D = (MU(K,2)*A+DEMU(K,2)*B+C)
      ATH2(K) = (MU(K,1)*A+DEMU(K,1)*B+V*C)/D
C
      A = DEMU(K,4) - DEMU(K,2)
      B = MU(K,2) - MU(K,4)
      C = MU(K,4)*DEMU(K,2) - MU(K,2)*DEMU(K,4)
      ATH3(K) = (MU(K,1)*A+DEMU(K,1)*B+V*C)/D
C
      A = DEMU(K,2) - DEMU(K,3)
      B = MU(K,3) - MU(K,2)
      C = MU(K,2)*DEMU(K,3) - MU(K,3)*DEMU(K,2)
      ATH4(K) = (MU(K,1)*A+DEMU(K,1)*B+V*C)/D
C
  310 CONTINUE
      ERRSAV = -1.
C
C CALCULATE ERROR IN SIMULATION FOR EACH SET
C OF RELATIVE THICKNESSES, AND SAVE THE ENERGY
C FOR WHICH THE ERROR IN THE TOTAL (LINEAR)
C ATTENUATION COEFFICIENT IS A MINIMUM.
C
      DO 325 I=1,NEN
      ATHX = ATH2(I)
      ATHY = ATH3(I)
      ATHZ = ATH4(I)
      SQER = 0.
      DO 320 K=1,NEN
      MU(K,5) = ATHX*MU(K,2) + ATHY*MU(K,3) + ATHZ*MU(K,4)
      TR(K) = MU(K,5)/MU(K,1)
      SQER = SQER + (1.-TR(K))**2
  320 CONTINUE
      IF (ERRSAV.LT.0) ERRSAV = SQER
      IF (SQER.LE.ERRSAV) THEN
      ERRSAV = SQER
      IESAV = I
      END IF
  325 CONTINUE
  330 CONTINUE
      EMAT = EN(IESAV)
      ATHX = ATH2(IESAV)
      ATHY = ATH3(IESAV)
      ATHZ = ATH4(IESAV)
  340 CONTINUE
C
C USING VALUES ATHX, ATHY, ATHZ, CALCULATE
C TOTAL AND PARTIAL COEFFICIENTS, AND RATIOS
C OF THESE TO THE CORRESPONDING VALUES FOR
C THE MATERIAL BEING SIMULATED.
C
C CALCULATE RATIO OF LINEAR
C ATTENUATION COEFFICIENTS
C
      SQER = 0.
      DO 350 K=1,NEN
      MU(K,5) = ATHX*MU(K,2) + ATHY*MU(K,3) + ATHZ*MU(K,4)
      TR(K) = MU(K,5)/MU(K,1)
      SQER = SQER + (1.-TR(K))**2
  350 CONTINUE
      SQER = SQRT(SQER/NEN)*100.
C
C CALCULATE RATIO OF EFFECTIVE
C INCOHERENT SCATTER COEFFICIENTS
C
      SQERIS = 0.
      DO 360 K=1,NEN
      MUIS(K,5) = MUIS(K,2)*ATHX + MUIS(K,3)*ATHY + MUIS(K,4)*ATHZ
      SIR(K) = MUIS(K,5)/MUIS(K,1)
      SQERIS = SQERIS + (1. -SIR(K))**2
  360 CONTINUE
      SQERIS = SQRT(SQERIS/NEN)*100.
C
C CALCULATE RATIO OF EFFECTIVE
C COHERENT SCATTER COEFFICIENTS
C
      SQERCS = 0.
      DO 370 K=1,NEN
      MUCS(K,5) = MUCS(K,2)*ATHX + MUCS(K,3)*ATHY + MUCS(K,4)*ATHZ
      SCR(K) = MUCS(K,5)/MUCS(K,1)
      SQERCS = SQERCS + (1. -SCR(K))**2
  370 CONTINUE
      SQERCS = SQRT(SQERCS/NEN)*100.
C
C CALCULATE RATIO OF EFFECTIVE
C PHOTOELECTRIC COEFFICIENTS
C
      SQERPE = 0.
      DO 380 K=1,NEN
      MUPE(K,5) = MUPE(K,2)*ATHX + MUPE(K,3)*ATHY + MUPE(K,4)*ATHZ
      PER(K) = MUPE(K,5)/MUPE(K,1)
      SQERPE = SQERPE + (1. -PER(K))**2
  380 CONTINUE
      SQERPE = SQRT(SQERPE/NEN)*100.
C
C CALCULATE RATIO OF EFFECTIVE
C TOTAL SCATTER COEFFICIENTS
C
      SQERS = 0.
      DO 390 K=1,NEN
      MUS(K,5) = MUS(K,2)*ATHX + MUS(K,3)*ATHY + MUS(K,4)*ATHZ
      SR(K) = MUS(K,5)/MUS(K,1)
      SQERS = SQERS + (1. -SR(K))**2
  390 CONTINUE
      SQERS = SQRT(SQERS/NEN)*100.
C
C CALCULATE RHO FOR NEW MATERIAL
C
      ATHT = ATHX + ATHY + ATHZ
      RHONM = ATHX*RHO(2) + ATHY*RHO(3) + ATHZ*RHO(4)
      RHO(5) = RHONM/ATHT
C
C CALCULATE MASS FRACTIONS FOR EACH MATERIAL
C
      AMX = ATHX*RHO(2)/RHONM
      AMY = ATHY*RHO(3)/RHONM
      AMZ = ATHZ*RHO(4)/RHONM
C
C CALCULATE MASS FRACTIONS FOR NEW MATERIAL
C
      N = 1
      DO 490 I=1,90
      Z(N,5) = I
      F(N,5) = 0.
      DO 480 IMAT=2,4
      DO 470 J=1,NZ(IMAT)
      IF (Z(J,IMAT).EQ.Z(N,5)) THEN
      IF (IMAT.EQ.2) THEN
      F(N,5) = F(N,5) + AMX*F(J,IMAT)
      ELSE IF (IMAT.EQ.3) THEN
      F(N,5) = F(N,5) + AMY*F(J,IMAT)
      ELSE IF (IMAT.EQ.4) THEN
      F(N,5) = F(N,5) + AMZ*F(J,IMAT)
      END IF
      GO TO 480
      END IF
  470 CONTINUE
  480 CONTINUE
      IF (F(N,5).NE.0.) THEN
      N = N + 1
      END IF
  490 CONTINUE
      NZ(5) = N-1
C
C ALL RATIOS HAVE BEEN CALCULATED
C WRITE RATIOS TO SHARED DISK FILES FOR PLOTTING
C
      REWIND(1)
	REWIND(2)
      DO 395 I=1,NEN
	WRITE(1,392)EN(I),TR(I),SR(I)
	WRITE(2,393)EN(I),SIR(I),SCR(I),PER(I)
  392 FORMAT(1X,F5.1,2X,F9.5,2X,F9.5)
  393 FORMAT(1X,F5.1,2X,F9.5,2X,F9.5,2X,F9.5)
  395 CONTINUE
C
C PRINT THICKNESS FRACTIONS FOR COMPONENTS
C OF SUBSTITUTE, AND RMS ERRORS.
C
  400 CONTINUE
      WRITE(*,1130)(NAME(I)(1:NL(I)),I=1,4),
     1 ATHX,ATHY,ATHZ
 1130 FORMAT(//,5X,'Simulation of ',A,'by ',A,', ',A,'and ',A,'.',
     2 /,20X,'(Ax,Ay,Az) = (',F7.4,',',F7.4,', and',F7.4,')')
      IF (EMAT.GT.0) WRITE(*,410)EMAT
  410 FORMAT(20X,'Match Energy =',F5.1)
      WRITE(*,420)V
  420 FORMAT(20X,'Relative Thickness =',F5.3)
      WRITE(*,430)SQER,SQERS,SQERPE,SQERIS,SQERCS
  430 FORMAT(/,11X,'Coefficient',8X,'RMS Fractional Error',//,
     19X,'Total Attenuation',8X,F10.4,' %',/,
     29X,'Total Scatter',12X,F10.4,' %',/,
     39X,'Photoelectric',12X,F10.4,' %',/,
     49X,'Compton Scatter',10X,F10.4,' %',/,
     59X,'Coherent Scatter',9X,F10.4,' %',/)
C
C NOW PRINT MATERIAL INFO
C
      WRITE(*,715)AMX,AMY,AMZ
  715 FORMAT(\,8X,'Relative Masses (Mx,My,Mz) = (',
     1  F7.4,',',F7.4,', and',F7.4,')',/)
      NL(5) = 9
      NAME(5)(1:9) = 'Composite'
      WRITE(*,720)(NAME(I)(1:NL(I)),I=1,5)
  720 FORMAT(/,3X,A,T20,A,T36,A,T52,A,T68,A,/)
      WRITE(*,730)
  730 FORMAT(2X,'Z',3X,'Wt.Fract.',
     1	     T19,'Z',3X,'Wt.Fract.',
     2	     T35,'Z',3X,'Wt.Fract.',
     3	     T51,'Z',3X,'Wt.Fract.',
     4	     T67,'Z',3X,'Wt.Fract.'/)
      NUM = NZ(1)
      IF (NZ(2).GT.NUM) NUM=NZ(2)
      IF (NZ(3).GT.NUM) NUM=NZ(3)
      IF (NZ(4).GT.NUM) NUM=NZ(4)
      IF (NZ(5).GT.NUM) NUM=NZ(5)
      DO 816 I=1,NUM
      IF (I.GT.NZ(1)) THEN
      WRITE(*,754)
  754 FORMAT(1X,11(' '),\)
      GO TO 750
      END IF
      WRITE(*,740)Z(I,1),F(I,1)
  740 FORMAT(1X,I2,3X,F6.4,\)
  750 IF (I.GT.NZ(2)) THEN
      WRITE(*,755)
  755 FORMAT(1X,15(' '),\)
      GO TO 770
      END IF
      WRITE(*,760)Z(I,2),F(I,2)
  760 FORMAT(5X,I2,3X,F6.4,\)
  770 IF (I.GT.NZ(3)) THEN
      WRITE(*,755)
      GO TO 790
      END IF
      WRITE(*,760)Z(I,3),F(I,3)
  790 IF (I.GT.NZ(4)) THEN
      WRITE(*,755)
      GO TO 810
      END IF
      WRITE(*,760)Z(I,4),F(I,4)
  810 IF (I.GT.NZ(5)) GO TO 814
      WRITE(*,780)Z(I,5),F(I,5)
  780 FORMAT(5X,I2,3X,F6.4)
      GO TO 816
  814 WRITE(*,815)
  815 FORMAT(1X)
  816 CONTINUE
      WRITE(*,818)(RHO(I),I=1,5)
  818 FORMAT(/,2X,5('Rho =',F6.3,5X))
      WRITE(*,1310)IMON,IDAY,IYR
 1310 FORMAT(/,18X,'Calculated on ',A,' ',A,', ',A,'.',/)
C
C CHANGE MATCH ENERGY OR THICKNESS FRACTIONS
C
      IF (IREP.NE.1) GOTO 460
      WRITE(*,435)
  435 FORMAT(///////,1X,'ENTER NEW VALUE FOR MATCH ENERGY',/,
     1	    1X,' OR 0 TO EXIT LOOP',/,
     2	    1X,'OR -1 TO CHOOSE Ax,Ay,Az,',/,
     3           1X,'OR -2 TO CHOOSE NEW RELATIVE THICKNESS')
      READ(*,*) ENEW
      IF (ENEW.EQ.0) GO TO 460
      IF (ENEW.EQ.-1) GO TO 440
      IF (ENEW.EQ.-2) GO TO 455
      IENER = (ENEW-EMIN)/DELTA + 1.0
      EMAT = EN(IENER)
      ATHX = ATH2(IENER)
      ATHY = ATH3(IENER)
      ATHZ = ATH4(IENER)
      GO TO 340
  440 WRITE(*,450)
  450 FORMAT(1X'ENTER Ax,Ay,Az')
      EMAT = ENEW
      READ(*,*)ATHX,ATHY,ATHZ
      GO TO 340
  455 WRITE(*,457)V
  457 FORMAT(1X,'CURRENT RELATIVE THICKNESS VALUE IS ',F8.5,/,
     1       1X,'ENTER NEW RELATIVE THICKNESS VALUE')
      READ(*,*)V
      GO TO 300
  460 CONTINUE
C
C SELECT OPTIONS---CHANGE MATCH ENERGY, NEW MATERIALS, STOP
C
  500 WRITE(*,510)
  510 FORMAT(///,1X,'SELECT ONE OF THE FOLLOWING:',/,
     11X,'1	SPECIFY NEW FRACTIONS OR MATCH ENERGY',/,
     21X,'2	SPECIFY DIFFERENT MATERIALS',/,
     31X,'3	RETURN TO MIN. RMS ERROR (TR. COEFF.) SOLUTION',/,
     41X,'4	STOP')
      READ(*,*)IREP
      GO TO (400,610,330,999) IREP
C
C CHANGE CANDIDATE MATERIALS
C
  610 WRITE(*,620)
  620 FORMAT(1X,'ENTER NUMBER OF MATERIAL TO CHANGE,',/,
     1	    1X,'OR 5 TO START CALCULATING WITH CHANGED MATERIALS')
      READ(*,*)IMAT
      IST = IMAT
      IEND = IMAT
      GO TO (100,100,100,100,300) IMAT
C
C PROGRAM END
C
  999 STOP
      END
