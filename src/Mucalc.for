      SUBROUTINE MUCALC(Z,ENER,S,DS,IZ)
C
C      THIS SUBROUTINE CALCULATES ATTENUATION COEFFICIENTS USING THE
C      POLYNOMIAL. FITS OF MCMASTER W.H., ET AL., 'COMPILATION OF X-RAY
C      CROSS SECTIONS' UCRL-50174-SEC.2 REV.1,MAY 1969.
C      CALLING PROGRAM PROVIDES ATOMIC NO. "Z" AND ENERGY "ENER" IN KEV
C	  THE SUBROUTINE GENERATES THE FOLLOWING:
C	*  COHERENT SCATTERING CROSS SECTION - 'S(1)'
C	*  ENERGY DERIVATIVE OF S(1) - 'DS(1)'
C	*  INCOHERENT SCATTERING CROSS SECTION - 'S(2)'
C	*  ENERGY DERIVATIVE OF S(2) - 'DS(2)'
C	*  SCATTERING CROSS SECTION (SUM OF S(1) + S(2)) - 'S(3)'
C	*  ENERGY DERIVATIVE OF S(3) - 'DS(3)'
C	*  PHOTOELECTRIC CROSS SECTION - 'S(4)'
C	*  ENERGY DERIVATIVE OF S(4) - 'DS(4)'
C	*  TOTAL CROSS SECTION - 'S(5)'
C	*  ENERGY DERIVATIVE OF S(5) - 'DS(5)'
C
C      ALL 'S' VALUES ARE GIVEN IN CM**2/GM.
C      ALL 'DS' VALUES ARE GIVEN IN CM**2/GM/KEV.
C
C	*  ELEMENTAL DENSITY - 'S(6)'
C	*  ELEMENTAL ATOMIC WEIGHT - 'S(7)'
C
C      DISC FILE XRAYMU.DAT CONTAINS 94 RECORDS
C      EACH OF WHICH CONTAINS 30 REAL NUMBERS,
C      ARRANGED IN THE FOLLOWING ORDER:
C      *  FIT COEFFICIENTS (24 REAL NUMBERS)
C	  OF THE LOG(MU)-LOG(E) POLYNOMIALS
C	  FOR EACH OF THE PARTIAL COEFFICIENTS.
C	  THE FIRST FOUR COEFFICIENTS ARE FOR
C	  COHERENT SCATTER, THE NEXT FOUR FOR
C	  COMPTON SCATTER, AND THE REMAINING
C	  FOUR SETS OF FOUR COEFFICIENTS EACH
C	  ARE FOR THE VARIOUS PIECES OF THE
C	  PHOTOELECTRIC EFFECT FUNCTION.  THE
C	  CORRECT SET IS SELECTED BY COMPARING
C	  THE ENERGY REQUESTED WITH THE ABSORPTION
C	  EDGE DATA, WHICH FOLLOW THE COEFFICIENTS.
C      *  ABSORPTION EDGES (3 REAL NUMBERS) IN KEV
C      *  CONVERSION FACTOR (REAL) TO CONVERT BARNS/ATOM TO CM**2/GM
C      *  ELEMENTAL DENSITY (REAL) IN GM/CM**3
C      *  ATOMIC WEIGHT (REAL)
C
      DIMENSION S(7),DS(5),BUF(30)
      INTEGER Z, PE
C
C INPUT PARAMETER CHECK
C
      IF (ENER.GT.1000.0.OR.ENER.LT.0.5) GO TO 400
      IF (Z.GT.94.OR.Z.LT.1) GO TO 400
C
C IF IZ=Z, DATA FOR THIS ELEMENT HAVE ALREADY
C BEEN READ FROM DISC, SO REPEATING THE READ
C IS UNNECESSARY. BECAUSE OF THIS FEATURE, THE
C CALLING PROGRAM SHOULD BE ARRANGED SO THAT
C ALL CALLS INVOLVING THE SAME ELEMENT SHOULD
C BE DONE AT THE SAME TIME. I.E., WHEN COEFFICIENTS
C ARE BEING CALCULATED AT MANY ENERGIES FOR SEVERAL
C ELEMENTS, THE LOOP OVER ENERGY SHOULD BE THE
C INNERMOST IN THE NESTING STRUCTURE.  IZ
C SHOULD BE SET TO 0 AT THE START OF THE
C CALLING PROGRAM TO ENSURE THAT THE DISC
C IS READ ON THE FIRST CALL TO MUCALC.
C
      IF (Z.EQ.IZ) GO TO 99
C
C DATA FOR THIS ELEMENT HAVE NOT BEEN READ,
C SO SET IZ=Z, AND READ THE DATA FROM DISC.
C
      IZ = Z
C
C THIS SECTION CONTAINS ALL OF THE DISC I/O
C FOR THE ENTIRE MATERIAL SIMULATION PROGRAM
C SCHEME. IT MUST BE ADAPTED TO SUIT THE
C COMPUTER SYSTEM ON WHICH THE PROGRAMS ARE
C TO BE USED.
C
C
C OPEN THE DISC FILE
C
      OPEN(UNIT=99,FILE='XRAYMU.DAT',
     1STATUS='OLD',ACCESS='DIRECT',
     2FORM='UNFORMATTED',RECL=30)
C
C READ THE ZTH RECORD
C
      READ(99,REC=Z)BUF
C
C CLOSE THE DISC FILE
C
      CLOSE(UNIT=99)
C
C THIS IS THE END OF THE DISC I/O SEQUENCE
C
C S(6) IS THE MASS DENSITY OF THE CURRENT ELEMENT
C
      S(6) = BUF(29)
C
C S(7) IS THE ATOMIC WEIGHT OF THE CURRENT ELEMENT
C THIS VALUE IS USED TO DETERMINE MASS FRACTIONS
C OF THE ELEMENTS OF A COMPOUND FROM THE CHEMICAL
C FORMULA, IN THE SUBROUTINE MATIN.
C
      S(7) = BUF(30)
C
C      SELECT P.E. COEFFICIENTS
C
   99 DO 100 I = 1,3
      IF (ENER.GT.BUF(I+24)) GO TO 100
      PE = I + 2
      GO TO 200
  100 CONTINUE
      PE = 6
C
C      CALCULATION OF THE MUSE
C
  200 CONTINUE
C
C CALCULATE THE NATURAL LOGARITHM OF THE ENERGY
C
      ELOG = ALOG(ENER)
C
      S(1) = EXP(BUF(1)+ELOG*(BUF(2)+ELOG*(BUF(3)+
     1		   ELOG*BUF(4))))/BUF(28)
C
      DS(1) = (BUF(2)+ELOG*(2.*BUF(3)+ELOG*3.*BUF(4)))
     1	      /ENER*S(1)
C
      S(2) = EXP(BUF(5)+ELOG*(BUF(6)+ELOG*(BUF(7)+
     1		   ELOG*BUF(8))))/BUF(28)
C
      DS(2) = (BUF(6)+ELOG*(2.*BUF(7)+ELOG*3.*BUF(8)))
     1	      /ENER*S(2)
C
      S(3) = S(1) + S(2)
C
      DS(3) = DS(1) + DS(2)
C
      NP = 4*(PE-1)
      S(4) = EXP(BUF(NP+1)+ELOG*(BUF(NP+2)+ELOG*(BUF(NP+3)+
     1		   ELOG*BUF(NP+4))))/BUF(28)
C
      DS(4) = (BUF(NP+2)+ELOG*(2.*BUF(NP+3)+ELOG*3.*BUF(NP+4)))
     1	      /ENER*S(4)
C
      S(5) = S(3) + S(4)
C
      DS(5) = DS(3) + DS(4)
C
  300 RETURN
C
C      PRINT ERROR MESSAGE AND RETURN TO MAIN PROGRAM
C
  400 WRITE(*,999)Z,ENER
  999 FORMAT(1X,'****** ERROR IN SUBROUTINE MUCALC FOR Z =',I5,
     1 5X,'ENERGY =',F10.4,' KEV******')
      RETURN
      END
